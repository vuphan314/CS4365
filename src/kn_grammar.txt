kn_prog(T) ::= kn_stats(T)

kn_stat(T) ::= unknown_stat(T)
kn_stat(T) ::= const_stat(T)
kn_stat(T) ::= fun_stat(T)
kn_stat(T) ::= check_stat(T)

unknown_stat(unknownStat T) ::= key_unknown kn_unknowns(T)

const_stat(constStat T T2) ::= key_const const_name(T) colon_eq kn_term(T2)

fun_stat(funStat T T2) ::= key_fun form_fun_expr(T) fun_body(T2)

fun_body(funBody T) ::= ret_cl(T)
fun_body(funBody T T2) ::= let_cls(T) ret_cl(T2)

let_cl(letCl T T2) ::= key_let tmp_name(T) colon_eq kn_term(T2)

ret_cl(retCl T) ::= key_ret kn_term(T)

check_stat(checkStat T T2) ::= key_check check_name(T) colon_eq kn_term(T2)

kn_term(condTerm T T2 T3) ::= kn_term(T) key_if bool_term(T2) key_else kn_term(T3)
kn_term(T) ::= bool_term(T)
kn_term(T) ::= ar_term(T)
kn_term(T) ::= inner_term(T)

bool_term(T) ::= bool_term_1(T)

bool_term_1(opOr T T2) ::= bool_term_1(T) op_or bool_term_2(T2)
bool_term_1(T) ::= bool_term_2(T)

bool_term_2(opAnd T T2) ::= bool_term_2(T) op_and bool_term_3(T2)
bool_term_2(T) ::= bool_term_3(T)

bool_term_3(opNot T) ::= op_not bool_term_4(T)
bool_term_3(T) ::= bool_term_4(T)

bool_term_4(T) ::= inner_bool_term(T)
bool_term_4(T) ::= compa_bool_term(T)

inner_bool_term(T) ::= key_truth(T)
inner_bool_term(T) ::= named_term(T)
inner_bool_term(T) ::= l_paren bool_term(T) r_paren

compa_bool_term(opEq T T2) ::= ar_term(T) op_eq ar_term(T2)
compa_bool_term(opUneq T T2) ::= ar_term(T) op_uneq ar_term(T2)
compa_bool_term(opGr T T2) ::= ar_term(T) op_gr ar_term(T2)
compa_bool_term(opLess T T2) ::= ar_term(T) op_less ar_term(T2)
compa_bool_term(grEq T T2) ::= ar_term(T) op_gr_eq ar_term(T2)
compa_bool_term(lessEq T T2) ::= ar_term(T) op_less_eq ar_term(T2)

ar_term(T) ::= ar_term_1(T)

ar_term_1(opPlus T T2) ::= ar_term_1(T) op_plus ar_term_2(T2)
ar_term_1(bMinus T T2) ::= ar_term_1(T) op_minus ar_term_2(T2)
ar_term_1(T) ::= ar_term_2(T)

ar_term_2(opMult T T2) ::= ar_term_2(T) op_mult ar_term_3(T2)
ar_term_2(opDiv T T2) ::= ar_term_2(T) op_div ar_term_3(T2)
ar_term_2(opMod T T2) ::= ar_term_2(T) op_mod ar_term_3(T2)
ar_term_2(T) ::= ar_term_3(T)

ar_term_3(uMinus T) ::= op_minus ar_term_4(T)
ar_term_3(T) ::= ar_term_4(T)

ar_term_4(opExp T T2) ::= ar_term_5(T) op_exp ar_term_3(T2)
ar_term_4(T) ::= ar_term_5(T)

ar_term_5(T) ::= inner_ar_term(T)

inner_ar_term(T) ::= kn_num(T)
inner_ar_term(T) ::= named_term(T)
inner_ar_term(T) ::= l_paren ar_term(T) r_paren

inner_term(T) ::= named_term(T)
inner_term(T) ::= l_paren kn_term(T) r_paren

kn_stats(knStats T) ::= kn_stat(T)
kn_stats(knStats T cut_root(Ts)) ::= kn_stat(T) kn_stats(Ts)

kn_unknowns(knUnknowns T) ::= unknown_name(T)
kn_unknowns(knUnknowns T cut_root(Ts)) ::= unknown_name(T) kn_comma kn_unknowns(Ts)

let_cls(letCls T) ::= let_cl(T)
let_cls(letCls T cut_root(Ts)) ::= let_cl(T) let_cls(Ts)

kn_terms(knTerms T) ::= kn_term(T)
kn_terms(knTerms T cut_root(Ts)) ::= kn_term(T) kn_comma kn_terms(Ts)

form_params(formParams T) ::= form_param(T)
form_params(formParams T cut_root(Ts)) ::= form_param(T) kn_comma form_params(Ts)

act_params(actParams T) ::= act_param(T)
act_params(actParams T cut_root(Ts)) ::= act_param(T) kn_comma act_params(Ts)

form_fun_expr(formFunExpr T) ::= fun_name(T) l_paren r_paren
form_fun_expr(formFunExpr T T2) ::= fun_name(T) l_paren form_params(T2) r_paren

act_fun_expr(actFunExpr T) ::= fun_name(T) l_paren r_paren
act_fun_expr(actFunExpr T T2) ::= fun_name(T) l_paren act_params(T2) r_paren

form_param(T) ::= kn_id(T)
act_param(T) ::= kn_term(T)

named_term(T) ::= unknown_name(T)
named_term(T) ::= const_name(T)
named_term(T) ::= fun_name(T)
named_term(T) ::= form_param(T)
named_term(T) ::= tmp_name(T)
named_term(T) ::= act_fun_expr(T)

unknown_name(T) ::= kn_id(T)
const_name(T) ::= kn_id(T)
fun_name(T) ::= kn_id(T)
tmp_name(T) ::= kn_id(T)
check_name(T) ::= kn_id(T)
